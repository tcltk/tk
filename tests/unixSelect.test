# This file contains tests for the tkUnixSelect.c file.
#
# This file contains a collection of tests for one or more of the Tcl
# built-in commands.  Sourcing this file into Tcl runs the tests and
# generates output for errors.  No output means no errors were found.
#
# Copyright © 1999 Scriptics Corporation.
#
# See the file "license.terms" for information on usage and redistribution
# of this file, and for a DISCLAIMER OF ALL WARRANTIES.

package require tcltest 2.2
namespace import ::tcltest::*
tcltest::configure {*}$argv
tcltest::loadTestedCommands

# Import utility procs for specific functional areas
testutils import child select

# Eliminate any existing selection on the screen.  This is needed in case
# there is a selection in some other application, in order to prevent races
# from causing false errors in the tests below.
selection clear .
after 1500

# set up a very large buffer to test INCR retrievals
set longValue ""
foreach i {a b c d e f g j h i j k l m o p q r s t u v w x y z} {
    set j $i.1$i.2$i.3$i.4$i.5$i.6$i.7$i.8$i.9$i.10$i.11$i.12$i.13$i.14
    append longValue A$j B$j C$j D$j E$j F$j G$j H$j I$j K$j L$j M$j N$j
}

# ----------------------------------------------------------------------

test unixSelect-1.1 {TkSelGetSelection procedure: simple i18n text} -constraints {
    x11
} -setup {
    destroy .e
    childTkProcess create
} -body {
    pack [entry .e]
    update
    .e insert 0 über
    .e selection range 0 end
    childTkProcess eval {string length [selection get]}
} -cleanup {
    childTkProcess exit
    destroy .e
} -result 4

test unixSelect-1.2 {TkSelGetSelection procedure: simple i18n text, iso8859-1} -constraints {
    x11
} -setup {
    childTkProcess create
} -body {
    childTkProcess eval {
	pack [entry .e]
	update
	.e insert 0 üф
	.e selection range 0 end
    }
    selection get
} -cleanup {
    childTkProcess exit
} -result ü?

test unixSelect-1.3 {TkSelGetSelection procedure: simple i18n text, iso2022} -constraints {
    x11
} -setup {
    childTkProcess create
    selectionSetup
} -body {
    selection handle -type COMPOUND_TEXT -format COMPOUND_TEXT . \
	{handler COMPOUND_TEXT}
    selection own .
    set selValue üф
    set selInfo {}
    set result [childTkProcess eval {
	set x [selection get -type COMPOUND_TEXT]
	list [string equal üф $x] [string length $x]
    }]
    lappend result $selInfo
} -cleanup {
    childTkProcess exit
} -result {1 2 {COMPOUND_TEXT 0 4000}}

test unixSelect-1.4 {TkSelGetSelection procedure: INCR i18n text, iso2022} -constraints {
    x11
} -setup {
    childTkProcess create
    selectionSetup
} -body {
    # This test is subtle.  The selection ends up getting fetched twice by
    # Tk:  once to compute the length, and again to actually send the data.
    # The first time through, we don't convert the data to ISO2022, so the
    # buffer boundaries end up being different in the two passes.
    selection handle -type COMPOUND_TEXT -format COMPOUND_TEXT . \
	{handler COMPOUND_TEXT}
    selection own .
    set selValue [string repeat x 3999]üф[string repeat x 3999]
    set selInfo {}
    set result [childTkProcess eval {
	set x [selection get -type COMPOUND_TEXT]
	list [string equal \
	    [string repeat x 3999]üф[string repeat x 3999] $x] \
	    [string length $x]
    }]
    lappend result $selInfo
} -cleanup {
    childTkProcess exit
} -result {1 8000 {COMPOUND_TEXT 0 4000 COMPOUND_TEXT 4000 3999 COMPOUND_TEXT 7998 4000 COMPOUND_TEXT 0 4000 COMPOUND_TEXT 4000 3998 COMPOUND_TEXT 7997 4000}}

test unixSelect-1.5 {TkSelGetSelection procedure: simple i18n text, iso2022} -constraints {
    x11
} -setup {
    childTkProcess create
    selectionSetup
} -body {
    selection handle -type COMPOUND_TEXT -format COMPOUND_TEXT . \
	{handler COMPOUND_TEXT}
    selection own .
    set selValue üф
    set selInfo {}
    set result [childTkProcess eval {
	set x [selection get -type COMPOUND_TEXT]
	list [string equal üф $x] [string length $x]
    }]
    lappend result $selInfo
} -cleanup {
    childTkProcess exit
} -result {1 2 {COMPOUND_TEXT 0 4000}}

test unixSelect-1.6 {TkSelGetSelection procedure: INCR i18n text} -constraints {
    x11
} -setup {
    childTkProcess create
} -body {
    childTkProcess eval [subst -nobackslashes {entry .e; pack .e; update
    .e insert 0 über$longValue
    .e selection range 0 end}]
    string length [selection get]
} -cleanup {
    childTkProcess exit
} -result [expr {4 + [string length $longValue]}]

test unixSelect-1.7 {TkSelGetSelection procedure: INCR i18n text} -constraints {
    x11
} -setup {
    childTkProcess create
} -body {
    childTkProcess eval {
	pack [entry .e]
	update
	.e insert 0 [string repeat x 3999]ü
	.e selection range 0 end
    }
    selection get
} -cleanup {
    childTkProcess exit
} -result [string repeat x 3999]ü

test unixSelect-1.8 {TkSelGetSelection procedure: INCR i18n text} -constraints {
    x11
} -setup {
    childTkProcess create
} -body {
    childTkProcess eval {
	pack [entry .e]
	update
	.e insert 0 ü[string repeat x 3999]
	.e selection range 0 end
    }
    selection get
} -cleanup {
    childTkProcess exit
} -result ü[string repeat x 3999]

test unixSelect-1.9 {TkSelGetSelection procedure: INCR i18n text} -constraints {
    x11
} -setup {
    childTkProcess create
} -body {
    childTkProcess eval {
	pack [entry .e]
	update
	.e insert 0 [string repeat x 3999]ü[string repeat x 4000]
	.e selection range 0 end
    }
    selection get
} -cleanup {
    childTkProcess exit
} -result [string repeat x 3999]ü[string repeat x 4000]
# Now some tests to make sure that the right thing is done when
# transferring UTF8 selections, to prevent [Bug 614650] and its ilk
# from rearing its ugly head again.

test unixSelect-1.10 {TkSelGetSelection procedure: INCR i18n text, utf-8} -constraints {
    x11
} -setup {
    childTkProcess create
} -body {
    childTkProcess eval {
	pack [entry .e]
	update
	.e insert 0 [string repeat x 3999]ü
	.e selection range 0 end
    }
    selection get -type UTF8_STRING
} -cleanup {
    childTkProcess exit
} -result [string repeat x 3999]ü

test unixSelect-1.11 {TkSelGetSelection procedure: INCR i18n text, utf-8} -constraints {
    x11
} -setup {
    childTkProcess create
} -body {
    childTkProcess eval {
	pack [entry .e]
	update
	.e insert 0 ü[string repeat x 3999]
	.e selection range 0 end
    }
    selection get -type UTF8_STRING
} -cleanup {
    childTkProcess exit
} -result ü[string repeat x 3999]

test unixSelect-1.12 {TkSelGetSelection procedure: INCR i18n text, utf-8} -constraints {
    x11
} -setup {
    childTkProcess create
} -body {
    childTkProcess eval {
	pack [entry .e]
	update
	.e insert 0 [string repeat x 3999]ü[string repeat x 4000]
	.e selection range 0 end
    }
    selection get -type UTF8_STRING
} -cleanup {
    childTkProcess exit
} -result [string repeat x 3999]ü[string repeat x 4000]

test unixSelect-1.13 {TkSelGetSelection procedure: simple i18n text, utf-8} -constraints {
    x11
} -setup {
    destroy .e
    childTkProcess create
} -body {
    pack [entry .e]
    update
    .e insert 0 überф
    .e selection range 0 end
    childTkProcess eval {string length [selection get -type UTF8_STRING]}
} -cleanup {
    destroy .e
    childTkProcess exit
} -result 5

test unixSelect-1.14 {TkSelGetSelection procedure: simple i18n text, utf-8} -constraints {
    x11
} -setup {
    childTkProcess create
} -body {
    childTkProcess eval {
	pack [entry .e]
	update
	.e insert 0 üф
	.e selection range 0 end
    }
    selection get -type UTF8_STRING
} -cleanup {
    childTkProcess exit
} -result üф

test unixSelect-1.15 {TkSelGetSelection procedure: INCR i18n text, utf-8} -constraints {
    x11
} -setup {
    childTkProcess create
} -body {
    childTkProcess eval {
	pack [entry .e]
	update
	.e insert 0 [string repeat [string repeat Ää 50]\n 21]
	.e selection range 0 end
    }
    selection get -type UTF8_STRING
} -cleanup {
    childTkProcess exit
} -result [string repeat [string repeat Ää 50]\n 21]

test unixSelect-1.16 {TkSelGetSelection procedure: INCR i18n text, utf-8} -constraints {
    x11
} -setup {
    childTkProcess create
} -body {
    childTkProcess eval {
	pack [entry .e]
	update
	.e insert 0 i[string repeat [string repeat Ää 50]\n 21]
	.e selection range 0 end
    }
    selection get -type UTF8_STRING
} -cleanup {
    childTkProcess exit
} -result i[string repeat [string repeat Ää 50]\n 21]

test unixSelect-1.17 {TkSelGetSelection procedure: INCR i18n text, utf-8} -constraints {
    x11
} -setup {
    childTkProcess create
} -body {
    childTkProcess eval {
	pack [text .t]
	update
	.t insert 1.0 [string repeat [string repeat Ää 50]\n 21]
	# Has to be selected in a separate stage
	.t tag add sel 1.0 21.end+1c
    }
    after 10
    selection get -type UTF8_STRING
} -cleanup {
    childTkProcess exit
} -result [string repeat [string repeat Ää 50]\n 21]

test unixSelect-1.18 {TkSelGetSelection procedure: INCR i18n text, utf-8} -constraints {
    x11
} -setup {
    childTkProcess create
} -body {
    childTkProcess eval {
	pack [text .t]
	update
	.t insert 1.0 i[string repeat [string repeat Ää 50]\n 21]
	# Has to be selected in a separate stage
	.t tag add sel 1.0 21.end+1c
    }
    after 10
    selection get -type UTF8_STRING
} -cleanup {
    childTkProcess exit
} -result i[string repeat [string repeat Ää 50]\n 21]

test unixSelect-1.19 {Automatic UTF8_STRING support for selection handle} -constraints {
    unix
} -setup {
    destroy .l
} -body {
    # See Bug #666346 "Selection handling crashes under KDE 3.0"
    label .l
    selection handle .l  [list handler STRING]
    set selValue "This is the selection value"
    selection own .l
    selection get -type UTF8_STRING
} -cleanup {
    destroy .l
} -result {This is the selection value}

#
# CLEANUP
#

testutils forget child select
cleanupTests
return
