#
# ttk::toggleswitch widget tests
#

#
# TESTFILE INITIALIZATION
#

package require tcltest 2.2; # needed for "-singleproc 0"

# Load the main script "main.tcl", which takes care of:
# - setup for the application and the root window
# - loading and configuration of the tcltest test harness
# - loading of the testutils mechanism together with its utility procs
# - loading of Tk specific test constraints (additionally to constraints
#   provided by the package tcltest)
tcltest::loadTestedCommands

#
# TESTS
#

test toggleswitch-1.1 "Toggleswitch" -body {
    pack [ttk::toggleswitch .sw -variable sw]
}

test toggleswitch-1.2 "Toggleswitch toggle" -body {
    .sw toggle
    list [set ::sw] [.sw instate selected] [expr {[.sw get] == [.sw get max]}]
} -result [list 1 1 1]

test toggleswitch-1.3 "Toggleswitch switchstate" -body {
    .sw switchstate 0
    list [set ::sw] [.sw instate selected] [expr {[.sw get] == [.sw get min]}]
} -result [list 0 0 1]

test toggleswitch-1.4 "Toggleswitch variable" -body {
    set result [list]
    set ::sw 1
    lappend result [.sw instate selected] [expr {[.sw get] == [.sw get max]}]
    set ::sw 0
    lappend result [.sw instate selected] [expr {[.sw get] == [.sw get min]}]
} -result {1 1 0 1}

test toggleswitch-1.5 "Unset toggleswitch variable" -body {
    set result [list]
    unset ::sw
    lappend result [.sw instate invalid] [info exists ::sw]
    set ::sw 1
    lappend result [.sw instate invalid] [info exists ::sw]
} -result {1 0 0 1}

test toggleswitch-1.6 "Toggleswitch default variable" -body {
    destroy .sw ; unset -nocomplain {} ; set result [list]
    ttk::toggleswitch .sw -onvalue on -offvalue off
    lappend result [.sw cget -variable] [info exists .sw] [.sw state]
    .sw toggle
    lappend result [info exists .sw] [set .sw] [.sw state]
    .sw toggle
    lappend result [info exists .sw] [set .sw] [.sw state]
} -result [list .sw 0 invalid  1 on selected  1 off {}]

test toggleswitch-1.7 "Toggleswitch empty variable" -body {
    # shall simply not crash
    destroy .sw
    ttk::toggleswitch .sw -variable {}
    .sw toggle
} -cleanup {
    destroy .sw
} -result {}

test toggleswitch-2.1 "style command" -body {
    ttk::toggleswitch .sw
    list [.sw cget -size] [.sw cget -style] [.sw style] [winfo class .sw]
} -cleanup {
    destroy .sw
} -result {2 Toggleswitch2 Toggleswitch2 Toggleswitch}

test toggleswitch-2.2 "-size and -style options" -body {
    ttk::toggleswitch .sw -size 3 -style customStyle.Toggleswitch3
    list [.sw cget -size] [.sw cget -style] [.sw style] [winfo class .sw]
} -cleanup {
    destroy .sw
} -result {3 customStyle.Toggleswitch3 customStyle.Toggleswitch3 Toggleswitch}

test toggleswitch-2.3 "-style option" -body {
    ttk::toggleswitch .sw
    .sw configure -style customStyle.Toggleswitch3
    list [.sw cget -size] [.sw cget -style] [.sw style] [winfo class .sw]
} -cleanup {
    destroy .sw
} -result {3 customStyle.Toggleswitch3 customStyle.Toggleswitch3 Toggleswitch}

test toggleswitch-2.4 "-size option takes precedence over -style" -body {
    ttk::toggleswitch .sw -style customStyle.Toggleswitch3   ;# intentionally 3
    list [.sw cget -size] [.sw cget -style] [.sw style] [winfo class .sw]
} -cleanup {
    destroy .sw
} -result {2 customStyle.Toggleswitch2 customStyle.Toggleswitch2 Toggleswitch}

#
# TESTFILE CLEANUP
#

tcltest::cleanupTests
