#
# ttk::radiobutton widget tests.
#

# NOTES
#
# * Many tests in this test file build upon a previous one. This occurs
#   systematically for groups of contiguous tests sharing the same major
#   test number. All test files for ttk widgets exhibit this practice.
#
#   As a consequence, attempts to run tests selectively/individually using the
#   tcltest options "-match", "-skip" or "-limitconstraints 1" lead to
#   unexpected failures. Therefore, invocations using these options should be
#   considered as "de facto invalid" for the ttk test suite.
#
#   As an exception, using "-match" or "-skip" to select or deselect entire
#   groups of contiguous tests does not seem to raise problems.
#

#
# TESTFILE INITIALIZATION
#

package require tcltest 2.2; # needed in mode -singleproc 0

# Load the main script main.tcl, which takes care of:
# - setup for the application and the root window
# - importing commands from the tcltest namespace
# - loading of the testutils mechanism along with its utility procs
# - loading of Tk specific test constraints (additionally to constraints
#   provided by the package tcltest)
source [file join [file dirname [tcltest::configure -testdir]] main.tcl]

# Ensure a pristine initial window state
resetWindows

#
# TESTS
#

test radiobutton-1.1 "Radiobutton check" -body {
    pack \
	[ttk::radiobutton .rb1 -text "One" -variable choice -value 1] \
	[ttk::radiobutton .rb2 -text "Two" -variable choice -value 2] \
	[ttk::radiobutton .rb3 -text "Three" -variable choice -value 3] \
	;
}
test radiobutton-1.2 "Radiobutton invoke" -body {
    .rb1 invoke
    set ::choice
} -result 1

test radiobutton-1.3 "Radiobutton state" -body {
    .rb1 instate selected
} -result 1

test radiobutton-1.4 "Other radiobutton invoke" -body {
    .rb2 invoke
    set ::choice
} -result 2

test radiobutton-1.5 "Other radiobutton state" -body {
    .rb2 instate selected
} -result 1

test radiobutton-1.6 "First radiobutton state" -body {
    .rb1 instate selected
} -result 0

test radiobutton-1.7 "Unset radiobutton variable" -body {
    unset ::choice
    list [info exists ::choice] [.rb1 instate alternate] [.rb2 instate alternate]
} -result {0 1 1}

test radiobutton-1.8 "Reset radiobutton variable" -body {
    set ::choice 2
    list [info exists ::choice] [.rb1 instate alternate] [.rb2 instate alternate]
} -result {1 0 0}

test radiobutton-2.1 "style command" -body {
    ttk::radiobutton .w
    list [.w cget -style] [.w style] [winfo class .w]
} -cleanup {
    destroy .w
} -result {{} TRadiobutton TRadiobutton}
test radiobutton-2.2 "style command" -body {
    ttk::style configure customStyle.TRadiobutton
    ttk::radiobutton .w -style customStyle.TRadiobutton
    list [.w cget -style] [.w style] [winfo class .w]
} -cleanup {
    destroy .w
} -result {customStyle.TRadiobutton customStyle.TRadiobutton TRadiobutton}

#
# TESTFILE CLEANUP
#

tcltest::cleanupTests
